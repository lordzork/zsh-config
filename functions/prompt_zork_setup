prompt_zork_help()
{
    cat <<EOF
We honor the following settings from the file $ZDOTDIR/.promptrc:

   ## the color of parentheses
   color_paren=blue

   ## the color of lables
   color_dark=white

   ## the color of variable data
   color_lite=WHITE

   ## the color of the user and host names
   color_userhost=BLUE

   ## the following should be left empty to disable features
   ##
   ## show a count of regular vs. hidden directories
   show_dirs=yes

   ## show a count of regular vs. hidden files
   show_files=yes

   ## show the number of the terminal device
   show_tty=yes

   ## show the command number/exit status of last command
   show_commands=yes

   ## use the short hostname instead of the fqdn
   show_shorthost=yes

   ## use military time
   clock=24
EOF
}

prompt_zork_preview()
{
    for n in 1 2 3 4 5 6 7 8; do
	psvar[$n]=""
    done
    
    prompt_preview_theme zork "$@"
}

prompt_zork_set_colors()
{
    case $2 in 
	BLACK|RED|GREEN|YELLOW|BLUE|MAGENTA|CYAN|WHITE)
	    clr[$1]="%{$reset_color%}%{$fg_bold[${(L)2}]%}"
	    ;;
	black|red|green|yellow|blue|magenta|cyan|white)
	    clr[$1]="%{$reset_color%}%{$fg[$2]%}"
	    ;;
    esac
}

prompt_zork_ls_count() 
{
    for n in 5 6 7 8; do
	psvar[$n]=""
    done

    local -a f hf d hd

    f=( *(.N) )
    hf=( .*(.N) )
    d=( *(/N) )
    hd=( .*(/N) )

    psvar[5]=${#f}
    psvar[6]=${#hf}
    psvar[7]=${#d}
    psvar[8]=${#hd}
}

prompt_zork_git_func()
{
    psvar[2,9]=""
    [ -d "$PWD/.git" ] || return

    local branch=$(git branch | grep \* | cut -d' ' -f2)

    psvar[9]=" [ git:$branch ]"

    case $(git --no-pager status) in
        *"nothing to commit"*)
            psvar[2]=" git$lp$rs$clr[lite]$branch$rp"
            ;;
        *"Untracked files"*)
	    psvar[2]=" git$lp$rs%{$fg_bold[red]%}$branch$rp"
	    ;;
	*)
            psvar[2]=" git$lp$rs%{$fg[red]%}$branch$rp"
            ;;
    esac
}

prompt_zork_is_console()
{
    case $TERM in
	xterm*|*rxvt*|Eterm*|cygwin*)
	    return 1 ;;
	*)
	    return 0
    esac
}

prompt_zork_preexec()
{
    prompt_zork_is_console && return

    titlebar_length=${TB_LENGTH:-$titlebar_length}
    case $1 in
        %*)
	    cmd=%%$1:gs/%//
	    ;;
        *)      
	    cmd="$(echo $1 | tr \\n " ")"
    esac

    print -Pn "%{\e]0;[ $host ] [ %n ] [ %D{$hr}:%D{%M}: %$titlebar_length>...>% $cmd%>> ]\a%}"
}

prompt_zork_precmd()
{
    prompt_zork_git_func
    prompt_zork_ls_count
 
    local jobs=
    psvar[1]=""
    local j=${(Mw)#jobstates#suspended:}
    if [ $j -gt 0 ]; then
	jobs=" [ $j+ ]"
	psvar[1]=( " $j+" )
    fi

    prompt_zork_is_console || print -Pn "%{\e]0;[ $host ] [ %n ] [ %30<...<%2c%<< ]%9v [ %h:%l ]$jobs\a%}"
}

prompt_zork_setup()
{
    autoload -Uz colors
    colors

    ## these variables can't be local because precmd() and 
    ## preexec() need them
    titlebar_length=50
    hr=%I
    host=%M

    local show_date=1
    local show_clock=1
    local show_files=1
    local show_dirs=1
    local show_commands=1
    local show_tty=1

    typeset -A clr    
    if [ "$UID" = 0 ]; then
	clr[paren]=%{$reset_color%}%{$fg_bold[blue]%}
	clr[dark]=%{$reset_color%}%{$fg[white]%}
	clr[lite]=%{$reset_color%}%{$fg_bold[white]%}
	clr[userhost]=%{$reset_color%}%{$fg_bold[red]%}
    else
	clr[paren]=%{$reset_color$fg[cyan]%}
	clr[dark]=%{$reset_color$fg_bold[black]%}
	clr[lite]=%{$reset_color$fg[white]%}
	clr[userhost]=$clr[paren]
    fi

    local str_clock=lc
    local str_date=d
    local str_com=com
    local str_git=git
    local str_dir=dirs
    local str_file=files
    local config=$ZDOTDIR/.promptrc

    . $config >/dev/null 2>&1

    local p
    for p in paren dark lite userhost; do
	prompt_zork_set_colors $p $(eval echo $`eval echo color_\\$p`)
	prompt_zork_set_colors $p $(eval echo $`eval echo clr_\\$p`)
	#local arg=${!color_$p}
	#set_colors $p $arg
    done

    [ "$show_shorthost" ] && host=%m
    [ "$clock" = 24 ]  && hr=%H

    rs="%{$reset_color%}"
    local lb="$clr[paren]["
    local rb="$clr[paren]]"
    lp="$clr[paren]($clr[lite]"
    rp="$clr[paren])"
    local slash="$clr[paren]/$clr[lite]"
    colon="$rs$clr[paren]:$clr[lite]"
    
    [ "$show_date" ] && local ps_date=" $clr[dark]$str_date$lp%D{%m}$slash%D{%d}$slash%D{%y}$rp"
    [ "$show_clock" ] && local ps_clock=" $clr[dark]$str_clock$lp%D{$hr}$colon%D{%M}$colon%D{%S}$rp"
    [ "$show_files" ] && local ps_file=" $clr[dark]$str_file$lp%5v$slash%6v$rp"
    [ "$show_dirs" ] && local ps_dir=" $clr[dark]$str_dir$lp%7v$slash%8v$rp"
    if [ "$show_tty" ]; then
	if prompt_zork_is_console; then
	    local str_tty=tty
	    local tty_num=$TTY:t
	else
	    local str_tty=$(echo $TTY | cut -d'/' -f3)
	    local tty_num=$TTY:t	    
	fi
	local ps_tty=" $clr[dark]$str_tty$lp$tty_num$rp$rs"
    fi
    [ "$show_commands" ] && local ps_com=" $clr[dark]$str_com$lp$clr[lite]%h$slash%?$rp"
    
    local ps_userhost="$clr[userhost]%n$clr[paren]@$clr[userhost]$host%b"

    local ps_wdir="$lp$clr[lite]%45<...<%~%<<$rp"

    local ps_line1="$lb$ps_date$ps_clock$ps_file$ps_dir$ps_tty$ps_com$clr[dark]"$'${psvar[2]}'"$clr[lite]%1v $rb"
    local ps_line2="$lb $ps_userhost$ps_wdir $rb"
    local ps_line3=" $clr[userhost]%#$clr[dark]-$clr[paren]>$rs"

    PS1="
${ps_line1}
${ps_line2}
${ps_line3} "
    PS2=" $clr[userhost]%#$clr[dark] %_$clr[dark]-$clr[paren]>%b%f%k "

    precmd() { prompt_zork_precmd "$@" }
    preexec() { prompt_zork_preexec "$@" }
}

prompt_zork_setup "$@"